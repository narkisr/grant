(* Entry point *)
sudoers ::= (<'Cmnd_Alias'> cmnd-alias | <'Defaults'> default-entry | user-spec | <empty-line>)*

empty-line = #'^\s*\n' |  #'^\n'

(* User alias *)

user-alias ::= name '=' user-list

(* User spec *)
user-spec ::= user-list host-list <'='> cmnd-spec-list (':' host-list <'='> cmnd-spec-list)*

(* Command aliases *)
alias-name ::= #'[A-Z]([A-Z]|[0-9]|\_)*'

cmnd-alias ::= alias-name <'='> cmnd-list  |  alias-name <'='> <'\\'> <'\n'> cmnd-list

file ::= #'(/[a-zA-Z0-9_-[.]]+)+'

wildcard ::= #'\?|\*|\+'

flag = (#'\-\w+' | #'\--\w+')

arg =  #'([a-zA-Z]|\d|\_|\=|\')+'

end-of-options = '--'

directory ::= #'(/[a-zA-Z0-9_-]+)+/' 

commandname ::= file (wildcard | flag | arg | file  | directory | '""'  | end-of-options)* |
                directory wildcard (arg | flag | file  | '""'  | end-of-options)*

cmnd-list ::= (cmnd {<','>} | cmnd {<','>} <'\\'> <'\n'> | cmnd <'\n'>)+

cmnd ::= '!'* commandname | '!'* "sudoedit" | '!'* alias-name <(#'\s*\n')?>


(* Runas *)

name = #'([A-Z]|[a-z]|[0-9]|\_|\-)+'

runas-alias ::= name <'='> runas-list

runas-list ::= runas-member | runas-member <','> runas-list

uid = #'\d+'

gid = #'\d+'

nonunix_group = gid

nonunix_gid = uid

netgroup = gid

group = name

runas-member ::= '!'* user |
                 '!'* '#' uid |
                 '!'* '%' group |
                 '!'* '%#' gid |
                 '!'* '%:' nonunix_group |
                 '!'* '%:#' nonunix_gid |
                 '!'* '+' netgroup |
                 '!'* alias-name

(* User Spec *)

user ::= #'([a-z]|\d|\_|\-)+'

user-list ::= (user | user <','> user-list)+

(* host spec *)

ip_addr = #'^((25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)\.){3}(25[0-5]|2[0-4][0-9]|[01]?[0-9][0-9]?)$'

hostname ::= #'([A-Z]|[a-z]|[0-9]|\_|\-)+'

host ::= '!'* hostname |
         '!'* ip_addr (*|
         '!'* network(/netmask)? |
         '!'* +netgroup |
         '!'* host-alias *)

host-list ::= host | host <','> host-list

cmnd-spec-list ::= (cmnd-spec | cmnd-spec <','>)+

cmnd-spec ::= runas-spec? selinux-spec? tag-spec* cmnd

runas-spec ::= <'('> runas-list? (<':'> runas-list)? <')'>

selinux-spec ::= ('ROLE=role' | 'TYPE=type')

tag-spec ::= ('EXEC:' | 'NOEXEC:' | 'FOLLOW:' | 'NOFOLLOW' |
              'LOG_INPUT:' | 'NOLOG_INPUT:' | 'LOG_OUTPUT:' |
              'NOLOG_OUTPUT:' | 'MAIL:' | 'NOMAIL:' | 'PASSWD:' |
              'NOPASSWD:' | 'SETENV:' | 'NOSETENV:')


(* Defaults *)
default-type ::= <'@'> host-list |
                 <':'> (user | alias-name) |
                 <'!'> cmnd-list |
                 <'>'> runas-list

default-entry ::= (default-type)? (parameter-list)+ <"\n">

parameter-list ::= parameter | parameter <','>

environment = #'[A-Z]([A-Z]|[0-9]|\_)*'

value = (user | file | environment)

identified = #'([a-z]|\_)+'

parameter ::= identified <'='>  value |
              identified <'+='> value  |
		  identified <'-='> value |
              '!'* identified

